%---- Cube Root Continued Fractions

% Source: F. Backeljauw, A. Cuyt, A Continued Fractions Package for Special Functions
% Powers, with alpha = 1/3

% Needed: scaling. Negative arguments? Monotonicity laws?

%---- Cube Root Upper Bound (n=1)

cnf(cbrt_upper_bound_1,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, (1/3)*X+2/3, Y)
    | lgen(R,cbrt(X),Y) )).

%---- Cube Root Upper Bounds (n=2)

%Argument scaled by a factor of 8: good near zero
cnf(cbrt_upper_bound_2_small,axiom,
    ( ~ 0 <= X
    | ~ X < 1
    | ~ lgen(R, (1/24)*(128*X^2+160*X+5)/(10*X+1), Y)
    | lgen(R,cbrt(X),Y) )).

cnf(cbrt_upper_bound_2,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, (1/3)*(2*X^2+20*X+5)/(5*X+4), Y)
    | lgen(R,cbrt(X),Y) )).

%Argument divided by 8: good for large X
cnf(cbrt_upper_bound_2_large,axiom,
    ( ~ 1 <= X
    | ~ lgen(R, (1/6)*(X^2+80*X+160)/(5*X+32), Y)
    | lgen(R,cbrt(X),Y) )).

%---- Cube Root Upper Bound (n=3)
% NOTE: this is highly accurate around 1, but is not the best for large X

cnf(cbrt_upper_bound_3,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, (1/9)*(20+7*X^3+210*X+168*X^2) / (10*X^2+28*X+7), Y)
    | lgen(R,cbrt(X),Y) )).
