%---- sinh Lower Bounds

% Based on Taylor series bounds for exponentials: exp_lower_taylor_5_cubed and exp_upper_bound_case_3
% Needed because the continued fraction bounds converge to small values for positive X.

cnf(sinh_taylor_lower_bound_pos1,axiom,
     ( ~ 1 <= X
     | ~ lgen(R, Y, (1/2)*(1+(1/3)*X+(1/18)*X^2+(1/162)*X^3+(1/1944)*X^4+(1/29160)*X^5)^3 - 1152/(X^3+6*X^2+24*X+48)^2)
     | lgen(R,Y,sinh(X)) )).

%---- These are based on continued fraction approximations to the exponential function.

% the first approximant
cnf(sinh_lower_bound_neg1,axiom,
     ( ~ X < 0
     | ~ -1.5 <= X
     | ~ lgen(R, Y, -4*X / ((X-2)*(X+2)))
     | lgen(R,Y,sinh(X)) )).

% the second approximant (valid for all positive X, but very poor beyond 2.5)
cnf(sinh_lower_bound_pos1,axiom,
     ( ~ 0 <= X
     | ~ X <= 2.5
     | ~ lgen(R, Y, 12*X*(X^2+12) / ((X^2-6*X+12)*(X^2+6*X+12)))
     | lgen(R,Y,sinh(X)) )).

%---- More Accurate Continued Fractions

% the third approximant
cnf(sinh_lower_bound_neg2,axiom,
     ( ~ X < 0
     | ~ -4 <= X
     | ~ lgen(R, Y, -24*X*(X^4+70*X^2+600) / ((X^3-12*X^2+60*X-120)*(X^3+12*X^2+60*X+120)))
     | lgen(R,Y,sinh(X)) )).

% the fourth approximant (valid for all positive X, but very poor beyond 5)
cnf(sinh_lower_bound_pos2,axiom,
     ( ~ 0 <= X
     | ~ X <= 5
     | ~ lgen(R, Y, 40*X*(70560+9240*X^2+222*X^4+X^6) / ((X^4-20*X^3+180*X^2-840*X+1680)*(X^4+20*X^3+180*X^2+840*X+1680)))
     | lgen(R,Y,sinh(X)) )).
