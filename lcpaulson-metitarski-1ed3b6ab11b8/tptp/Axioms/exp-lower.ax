%----  Exp Lower Bound Definitions

cnf(exp_positive,axiom,
    ( ~ Y <= 0
    | lgen(R,Y,exp(X)) )).

%A Dictionary of Inequalities, P. S. Bullen, 1998, page 83:
% exp(x) > 1 + x + ... + x^n/n!  provided n is odd and x <> 0.

% Note also that x<=y implies x^k <= y^k for odd k.

% Taylor series up to term 1
% Valid for all X; the restriction excludes a particularly inaccurate part of its range
cnf(exp_lower_taylor_1,axiom,
    ( ~ -1 <= X
    | ~ lgen(R,Y,1+X)
    | lgen(R,Y,exp(X)) )).

% Based on the Taylor series up to term 5
% Formerly restricted to -3<=X<=-2, but it holds for all X. OK on [-7,2]: 0.0024
cnf(exp_lower_taylor_5_cubed,axiom,
    ( ~ lgen(R, Y, (1 + X/3 + (1/2)*(X/3)^2 + (1/6)*(X/3)^3 + (1/24)*(X/3)^4 + (1/120)*(X/3)^5)^3)
    | lgen(R,Y,exp(X)) )).

%----  Exp Continued Fractions

% Very close in the interval [0,1]: 4.8e-13; for [0,3] it's 6.6e-6
cnf(exp_lower_bound_cf2,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, Y, (X^2 + 6*X + 12) / (X^2 - 6*X + 12))
    | lgen(R,Y,exp(X)) )).

cnf(exp_lower_bound_cf3,axiom,
    ( 0 <= X
    | ~ lgen(R, Y, -(X^3 + 12*X^2 + 60*X + 120) / (X^3 - 12*X^2 + 60*X - 120))
    | lgen(R,Y,exp(X)) )).
