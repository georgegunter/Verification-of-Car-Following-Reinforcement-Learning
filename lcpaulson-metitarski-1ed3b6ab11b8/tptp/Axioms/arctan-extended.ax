%---- Atan Upper Bound Definitions (high accuracy)
% Source: F. Backeljauw, A. Cuyt, A Continued Fractions Package for Special Functions
% Bounds for |x|>1 are based on the formula   arctan x = pi/2 - arctan (1/x)
% Bounds for x < 0 are based on the formula   arctan x = - arctan (-x)

% the seventh approximant (reciprocal)
% valid for all X<0, but relaxing the first literal harms performance
cnf(atan_upper_bound_case_41,axiom,
     ( ~ X < -1
     | ~ lgen(R, -1.57079632-(1/35)*(256+5943*X^2+19250*X^4+15015*X^6)/(X*(35+315*X^2+693*X^4+429*X^6)), Y)
     | lgen(R,arctan(X),Y) )).

% the eighth approximant
cnf(atan_upper_bound_case_42,axiom,
     ( ~ X < 0
     | ~ lgen(R, (1/35)*(15159*X^6+147455*X^4+345345*X^2+225225)*X/(35*X^8+1260*X^6+6930*X^4+12012*X^2+6435), Y)
     | lgen(R,arctan(X),Y) )).

% the seventh approximant
cnf(atan_upper_bound_case_43,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, (1/35)*(256*X^6+5943*X^4+19250*X^2+15015)*X/(35*X^6+315*X^4+693*X^2+429), Y)
     | lgen(R,arctan(X),Y) )).

% the eighth approximant (reciprocal)
% valid for all X, but omitting the first literal harms performance
cnf(atan_upper_bound_case_44,axiom,
     ( ~ 1 < X
     | ~ lgen(R, 1.57079633-(1/35)*(15159+147455*X^2+345345*X^4+225225*X^6)*X/(35+1260*X^2+6930*X^4+12012*X^6+6435*X^8), Y)
     | lgen(R,arctan(X),Y) )).

%---- Atan Lower Bound Definitions
% Source: F. Backeljauw, A. Cuyt, A Continued Fractions Package for Special Functions

% the eighth approximant (reciprocal)
% valid for all X, but omitting the first literal harms performance
cnf(atan_lower_bound_case_41,axiom,
     ( ~ X < -1
     | ~ lgen(R, Y, -1.57079633-(1/35)*(15159+147455*X^2+345345*X^4+225225*X^6)*X/(35+1260*X^2+6930*X^4+12012*X^6+6435*X^8))
     | lgen(R,Y,arctan(X)) )).

% the seventh approximant
cnf(atan_lower_bound_case_42,axiom,
     ( ~ X < 0
     | ~ lgen(R, Y, (1/35)*(256*X^6+5943*X^4+19250*X^2+15015)*X/(35*X^6+315*X^4+693*X^2+429))
     | lgen(R,Y,arctan(X)) )).

% the eighth approximant
cnf(atan_lower_bound_case_43,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, Y, (1/35)*(15159*X^6+147455*X^4+345345*X^2+225225)*X/(35*X^8+1260*X^6+6930*X^4+12012*X^2+6435))
     | lgen(R,Y,arctan(X)) )).

% the seventh approximant (reciprocal)
% valid for all X<0, but relaxing the first literal harms performance
cnf(atan_lower_bound_case_44,axiom,
     ( ~ 1 < X
     | ~ lgen(R, Y, 1.57079632-(1/35)*(256+5943*X^2+19250*X^4+15015*X^6)/(X*(35+315*X^2+693*X^4+429*X^6)))
     | lgen(R,Y,arctan(X)) )).


