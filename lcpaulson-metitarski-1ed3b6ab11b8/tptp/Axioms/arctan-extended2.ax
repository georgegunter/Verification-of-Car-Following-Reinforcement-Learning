%---- Atan Upper Bound Definitions (very high accuracy)
% Source: F. Backeljauw, A. Cuyt, A Continued Fractions Package for Special Functions
% Bounds for |x|>1 are based on the formula   arctan x = pi/2 - arctan (1/x)
% Bounds for x < 0 are based on the formula   arctan x = - arctan (-x)

% the ninth approximant (reciprocal)
% valid for all X<0, but relaxing the first literal harms performance
cnf(atan_upper_bound_case_51,axiom,
     ( ~ X < -1
     | ~ lgen(R, -1.570796326 - (1/315)*(16384+638055*X^2+3738735*X^4+6831825*X^6+3828825*X^8)/(X*(315+4620*X^2+18018*X^4+25740*X^6+12155*X^8)), Y)
     | lgen(R,arctan(X),Y) )).

% the tenth approximant
cnf(atan_upper_bound_case_52,axiom,
     ( ~ X < 0
     | ~ lgen(R, (11/315)*(27985*X^8+437580*X^6+1800162*X^4+2691780*X^2+1322685)*X/(63*X^10+3465*X^8+30030*X^6+90090*X^4+109395*X^2+46189), Y)
     | lgen(R,arctan(X),Y) )).

% the ninth approximant
cnf(atan_upper_bound_case_53,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, (1/315)*(16384*X^8+638055*X^6+3738735*X^4+6831825*X^2+3828825)*X/(315*X^8+4620*X^6+18018*X^4+25740*X^2+12155), Y)
     | lgen(R,arctan(X),Y) )).

% the tenth approximant (reciprocal)
% valid for all X, but omitting the first literal harms performance
cnf(atan_upper_bound_case_54,axiom,
     ( ~ 1 < X
     | ~ lgen(R, 1.570796327 - (11/315)*(27985+437580*X^2+1800162*X^4+2691780*X^6+1322685*X^8)*X/(63+3465*X^2+30030*X^4+90090*X^6+109395*X^8+46189*X^10), Y)
     | lgen(R,arctan(X),Y) )).

%---- Atan Lower Bound Definitions
% Source: F. Backeljauw, A. Cuyt, A Continued Fractions Package for Special Functions

% the tenth approximant (reciprocal)
% valid for all X, but omitting the first literal harms performance
cnf(atan_lower_bound_case_51,axiom,
     ( ~ X < -1
     | ~ lgen(R, Y, -1.5707963267 - (11/315)*(27985+437580*X^2+1800162*X^4+2691780*X^6+1322685*X^8)*X/(63+3465*X^2+30030*X^4+90090*X^6+109395*X^8+46189*X^10))
     | lgen(R,Y,arctan(X)) )).

% the ninth approximant
cnf(atan_lower_bound_case_52,axiom,
     ( ~ X < 0
     | ~ lgen(R, Y, (1/315)*(16384*X^8+638055*X^6+3738735*X^4+6831825*X^2+3828825)*X/(315*X^8+4620*X^6+18018*X^4+25740*X^2+12155))
     | lgen(R,Y,arctan(X)) )).

% the tenth approximant
cnf(atan_lower_bound_case_53,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, Y, (11/315)*(27985*X^8+437580*X^6+1800162*X^4+2691780*X^2+1322685)*X/(63*X^10+3465*X^8+30030*X^6+90090*X^4+109395*X^2+46189))
     | lgen(R,Y,arctan(X)) )).

% the ninth approximant (reciprocal)
% valid for all X<0, but relaxing the first literal harms performance
cnf(atan_lower_bound_case_54,axiom,
     ( ~ 1 < X
     | ~ lgen(R, Y, 1.570796326 - (1/315)*(16384+638055*X^2+3738735*X^4+6831825*X^6+3828825*X^8)/(X*(315+4620*X^2+18018*X^4+25740*X^6+12155*X^8)))
     | lgen(R,Y,arctan(X)) )).


%%%%%%%%% level 6 %%%%%%%%%%

% the 11th approximant
cnf(atan_upper_bound_case_63,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, (1/3465)*(327680*X^10+19225635*X^8+175855680*X^6+552473922*X^4+698377680*X^2+305540235)*X/(693*X^10+15015*X^8+90090*X^6+218790*X^4+230945*X^2+88179), Y)
     | lgen(R,arctan(X),Y) )).

% the 12th approximant
cnf(atan_lower_bound_case_63,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, Y, (13/3465)*(1148325*X^10+26272015*X^8+165146058*X^6+417683574*X^4+457004625*X^2+180190395)*X/(231*X^12+18018*X^10+225225*X^8+1021020*X^6+2078505*X^4+1939938*X^2+676039))
     | lgen(R,Y,arctan(X)) )).
