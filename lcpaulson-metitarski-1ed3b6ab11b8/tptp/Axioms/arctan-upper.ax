%---- Atan Upper Bound Definitions
% Bounds for |x|>1 are based on the formula   arctan x = pi/2 - arctan (1/x)
% Bounds for x < 0 are based on the formula   arctan x = - arctan (-x)

%---- Atan Continued Fractions
% Source: F. Backeljauw, A. Cuyt, A Continued Fractions Package for Special Functions

% the first approximant (reciprocal)
% valid for all X<0, but relaxing the first literal harms performance
cnf(atan_upper_bound_case_11,axiom,
     ( ~ X < -1
     | ~ lgen(R, -1.57079 - 1/X, Y)
     | lgen(R,arctan(X),Y) )).

% the second approximant
cnf(atan_upper_bound_case_12,axiom,
     ( ~ X < 0
     | ~ lgen(R, 3*X/(X^2+3), Y)
     | lgen(R,arctan(X),Y) )).

% the first approximant
cnf(atan_upper_bound_case_13,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, X, Y)
     | lgen(R,arctan(X),Y) )).

% the second approximant (reciprocal)
% valid for all X, but omitting the first literal harms performance
cnf(atan_upper_bound_case_14,axiom,
     ( ~ 1 < X
     | ~ lgen(R, 1.57080 - 3*X/(1+3*X^2), Y)
     | lgen(R,arctan(X),Y) )).

%---- More Accurate Continued Fractions

% the fifth approximant (reciprocal)
% valid for all X<0, but relaxing the first literal harms performance
cnf(atan_upper_bound_case_31,axiom,
     ( ~ X < -1
     | ~ lgen(R, -1.57079 - (1/15)*(64+735*X^2+945*X^4)/(X*(15+70*X^2+63*X^4)), Y)
     | lgen(R,arctan(X),Y) )).

% the sixth approximant
cnf(atan_upper_bound_case_32,axiom,
     ( ~ X < 0
     | ~ lgen(R, (7/5)*(33*X^4+170*X^2+165)*X/(5*X^6+105*X^4+315*X^2+231), Y)
     | lgen(R,arctan(X),Y) )).

% the fifth approximant
cnf(atan_upper_bound_case_33,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, (1/15)*(64*X^4+735*X^2+945)*X/(15*X^4+70*X^2+63), Y)
     | lgen(R,arctan(X),Y) )).

% the sixth approximant (reciprocal)
% valid for all X, but omitting the first literal harms performance
cnf(atan_upper_bound_case_34,axiom,
     ( ~ 1 < X
     | ~ lgen(R, 1.57080 - (7/5)*(33+170*X^2+165*X^4)*X/(5+105*X^2+315*X^4+231*X^6), Y)
     | lgen(R,arctan(X),Y) )).

