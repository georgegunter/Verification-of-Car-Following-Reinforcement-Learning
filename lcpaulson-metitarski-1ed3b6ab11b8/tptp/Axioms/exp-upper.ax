%----  Exp Upper Bound Definitions

%A Dictionary of Inequalities, P. S. Bullen, 1998, page 83:
% exp(x) < 1 + x + ... + x^n/n!  provided n is even and x < 0.

% Positive upper bounds hold because exptay(-x,n) <= exp(-x) = 1/exp(x) for odd n and all x.
% Therefore, exp(x) <= 1/exptay(-x,n) provided the denominator is positive.
% For n=3, the denominator goes to zero at 2 * sinh(asinh(1)/3) + 1 = 1.596 and is
%   positive below this point.
% For n=5, the denominator goes to zero at 2.18. The Taylor-based upper bounds are also
%   valid for negative x because exp(x) >= 1 + x + ... + x^n/n!  provided n is odd.

% Square of reciprocal Taylor series up to term 3: exptaylor(x/2,3)^2. Exact at X=0.
% Poor in the interval [0,1]: infnorm is 0.021
% Valid up to X<3, but restricting the axiom as shown improves performance for hard problems
cnf(exp_upper_bound_case_3,axiom,
    ( ~ X <= 0
    | ~ lgen(R, 2304 / (-(X^3) + 6*X^2 - 24*X + 48)^2, Y)
    | lgen(R,exp(X),Y) )).

% Fourth power of reciprocal Taylor series up to term 3: exptaylor(x/4,3)^4. Exact at X=0.
% Within [0,1], infnorm is 4.8e-13; for [0,3] it's 6.63e-6; it gets much worse!
% Valid up to X<6, but restricting the axiom as shown improves performance for hard problems
cnf(exp_upper_bound_case_5,axiom,
    ( ~ X <= 3
    | ~ lgen(R, 21743271936 / (-(X^3) + 12*X^2 - 96*X + 384)^4, Y)
    | lgen(R,exp(X),Y) )).

%----  Exp Continued Fractions

% This simple bound is beneficial with nested functions. Diverges at X = 2.
cnf(exp_upper_bound_cf1, axiom,
    (X < 0 | 3/2 < X |
     ~ lgen(R, -(X+2)/(X-2), Y) | lgen(R, exp(X), Y))).

% Valid for X<0.
cnf(exp_upper_bound_cf2,axiom,
    ( 0 <= X
    | ~ lgen(R, (X^2 + 6*X + 12) / (X^2 - 6*X + 12), Y)
    | lgen(R,exp(X),Y) )).

% Actually diverges at X = 4.644.
cnf(exp_upper_bound_cf3,axiom,
    ( ~ 0 <= X
    | ~ X <= 4
    | ~ lgen(R, -(X^3 + 12*X^2 + 60*X + 120) / (X^3 - 12*X^2 + 60*X - 120), Y)
    | lgen(R,exp(X),Y) )).

