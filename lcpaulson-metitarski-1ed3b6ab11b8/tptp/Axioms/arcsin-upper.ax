%---- Arcsin Upper Bound Definitions
% Source: F. Backeljauw, A. Cuyt, A Continued Fractions Package for Special Functions

%---- Arcsin Continued Fractions, for positive X

% the first approximant
cnf(arcsin_upper_bound_pos_1,axiom,
     ( ~ 0 <= X 
     | ~ X < 1 
     | ~ lgen(R, X/sqrt(1-X^2), Y)
     | lgen(R,arcsin(X),Y) )).

% Theorem 1.6 of Ling Zhu. New Inequalities of Shafer-Fink Type for Arc Hyperbolic Sine.
% Journal of Inequalities and Applications (2008). doi:10.1155/2008/368275
% Simpler but more accurate than the continued fraction bounds shown below
% The exact bound is (pi/(pi-2))*X / ((2/(pi-2)) + sqrt(1-X^2))
cnf(arcsin_upper_bound_pos_2,axiom,
     ( ~ 0 <= X 
     | ~ X < 1 
     | ~ lgen(R, 2.752*X / (1.7519 + sqrt(1-X^2)), Y)
     | lgen(R,arcsin(X),Y) )).

%% the third approximant
%cnf(arcsin_upper_bound_pos_2,axiom,
%     ( ~ 0 <= X 
%     | ~ X < 1 
%     | ~ lgen(R, (1/3)*(11*X^2-15)*X / (sqrt(1-X^2)*(2*X^2-5)), Y)
%     | lgen(R,arcsin(X),Y) )).
%
%% the fifth approximant
%cnf(arcsin_upper_bound_pos_3,axiom,
%     ( ~ 0 <= X 
%     | ~ X < 1 
%     | ~ lgen(R, (1/15)*(274*X^4-1155*X^2+945)*X / (sqrt(1-X^2)*(8*X^4-56*X^2+63)), Y)
%     | lgen(R,arcsin(X),Y) )).
%
%% the seventh approximant
%cnf(arcsin_upper_bound_pos_4,axiom,
%     ( ~ 0 <= X 
%     | ~ X < 1 
%     | ~ lgen(R, (1/35)*(1452*X^6-12488*X^4+25795*X^2-15015)*X / (sqrt(1-X^2)*(16*X^6-216*X^4+594*X^2-429)), Y)
%     | lgen(R,arcsin(X),Y) )).

%---- The Maclaurin series for the inverse sine, for negative X

% the first term
cnf(arcsin_upper_bound_neg_1,axiom,
     ( ~ -1 <= X 
     | ~ X <= 0 
     | ~ lgen(R, (1/6)*X^3 + X, Y)
     | lgen(R,arcsin(X),Y) )).

% the second term
cnf(arcsin_upper_bound_neg_2,axiom,
     ( ~ -1 <= X 
     | ~ X <= 0
     | ~ lgen(R, (3/40)*X^5 + (1/6)*X^3 + X, Y)
     | lgen(R,arcsin(X),Y) )).

% the third term
cnf(arcsin_upper_bound_neg_3,axiom,
     ( ~ -1 <= X 
     | ~ X <= 0 
     | ~ lgen(R, (5/112)*X^7 + (3/40)*X^5 + (1/6)*X^3 + X, Y)
     | lgen(R,arcsin(X),Y) )).

% the fifth term
cnf(arcsin_upper_bound_neg_5,axiom,
     ( ~ -1 <= X 
     | ~ X <= 0 
     | ~ lgen(R, (63/2816)*X^11 + (35/1152)*X^9 + (5/112)*X^7 + (3/40)*X^5 + (1/6)*X^3 + X, Y)
     | lgen(R,arcsin(X),Y) )).
