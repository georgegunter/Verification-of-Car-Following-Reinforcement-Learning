%---- Sqrt Lower Bounds (Based on Newton's Method)

% These bounds are not especially accurate at zero, which causes difficulties with some problems.

% 04-04-2008: checked using Maple
% 09-04-2008: made the bounds more precise, and exact at 1

% This obvious fact benefits some proofs.
fof(sqrt_squared, axiom, (X < 0 | sqrt(X)*sqrt(X) = X)).

% The case for this linear bound is unclear: some proofs become more complex.
cnf(sqrt_lower_bound_id,axiom,
    ( ~ 0 <= X
    | ~ X <= 1
    | ~ lgen(R, Y, X)
    | lgen(R,Y,sqrt(X)) )).

%---- Sqrt Lower Bound (n=0)

cnf(sqrt_lower_bound_0,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, Y, 2*X/(X+1))
    | lgen(R,Y,sqrt(X)) )).

%---- Sqrt Lower Bound (n=2)
% 04-04-2008: corrected an error

%Argument scaled by a factor of 16: good near zero
cnf(sqrt_lower_bound_2_small,axiom,
    ( ~ 0 <= X
    | ~ X < 1
    | ~ lgen(R, Y, 32*X*(16*X+1)*(256*X^2+96*X+1)/(65536*X^4+114688*X^3+17920*X^2+448*X+1))
    | lgen(R,Y,sqrt(X)) )).

cnf(sqrt_lower_bound_2,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, Y, 8*X*(X+1)*(X^2+6*X+1)/(X^4+28*X^3+70*X^2+28*X+1))
    | lgen(R,Y,sqrt(X)) )).

%Argument divided by 16: good for large X
cnf(sqrt_lower_bound_2_large,axiom,
    ( ~ 1 <= X
    | ~ lgen(R, Y, 32*X*(X+16)*(X^2+96*X+256)/(X^4+448*X^3+17920*X^2+114688*X+65536))
    | lgen(R,Y,sqrt(X)) )).

