%---- sinh Upper Bounds

% Based on Taylor series bounds for exponentials: exp_lower_taylor_5_cubed and exp_upper_bound_case_3
% Needed because the continued fraction bounds converge to small values for positive X.

% Avoiding an overlap with other bounds improves efficiency
cnf(sinh_taylor_upper_bound_neg1,axiom,
     ( ~ X <= -1 
     | ~ lgen(R, Y, -(1/2)*(1-(1/3)*X+(1/18)*X^2-(1/162)*X^3+(1/1944)*X^4-(1/29160)*X^5)^3 + 1152/(-X^3+6*X^2-24*X+48)^2)
     | lgen(R,sinh(X),Y) )).

%---- These are based on continued fraction approximations to the exponential function.

% the first approximant
cnf(sinh_upper_bound_pos1,axiom,
     ( ~ 0 <= X
     | ~ X <= 1.5
     | ~ lgen(R, -4*X / ((X-2)*(X+2)), Y)
     | lgen(R,sinh(X),Y) )).

% the second approximant (valid for all negative X, but very poor beyond -2.5)
cnf(sinh_upper_bound_neg1,axiom,
     ( ~ X < 0 
     | ~ -2.5 <= X
     | ~ lgen(R, 12*X*(X^2+12) / ((X^2-6*X+12)*(X^2+6*X+12)), Y)
     | lgen(R,sinh(X),Y) )).

%---- More Accurate Continued Fractions

% the third approximant
cnf(sinh_upper_bound_pos2,axiom,
     ( ~ 0 <= X
     | ~ X <= 4
     | ~ lgen(R, -24*X*(X^4+70*X^2+600) / ((X^3-12*X^2+60*X-120)*(X^3+12*X^2+60*X+120)), Y)
     | lgen(R,sinh(X),Y) )).

% the fourth approximant (valid for all negative X, but very poor beyond -2.5)
cnf(sinh_upper_bound_neg2,axiom,
     ( ~ X < 0
     | ~ -5 <= X
     | ~ lgen(R, 40*X*(70560+9240*X^2+222*X^4+X^6) / ((X^4-20*X^3+180*X^2-840*X+1680)*(X^4+20*X^3+180*X^2+840*X+1680)), Y)
     | lgen(R,sinh(X),Y) )).
