%---- Sin Upper and Lower Bounds (Taylor Series Approximants)

%-- Upper bounds (n=0)

cnf(sin_upper_bound_0_neg,axiom,
    ( ~ X <= 0
    | ~ lgen(R, X - (X^3)/6, Y)
    | lgen(R, sin(X), Y) )).

cnf(sin_upper_bound_0_pos,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, X, Y)
    | lgen(R, sin(X), Y) )).

%-- Upper bounds (n=2)

% accurate to .104 for x = 0 .. 4.8
cnf(sin_upper_bound_2_neg,axiom,
    ( ~ X <= 0
    | ~ lgen(R, X - (X^3)/6 + (X^5)/120 - (X^7)/5040 + (X^9)/362880 - (X^11)/39916800, Y)
    | lgen(R, sin(X), Y) )).

% accurate to .095 for x = 0 .. 4
cnf(sin_upper_bound_2_pos,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, X - (X^3)/6 + (X^5)/120 - (X^7)/5040 + (X^9)/362880, Y)
    | lgen(R, sin(X), Y) )).


%-- Lower bounds (n=0)

cnf(sin_lower_bound_0_neg,axiom,
    ( ~ X <= 0
    | ~ lgen(R, Y, X)
    | lgen(R, Y, sin(X)) )).

cnf(sin_lower_bound_0_pos,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, Y, X - (X^3)/6)
    | lgen(R, Y, sin(X)) )).

%-- Lower bounds (n=2)

% accurate to .095 for x = -4 .. 0
cnf(sin_lower_bound_2_neg,axiom,
    ( ~ X <= 0
    | ~ lgen(R, Y, X - (X^3)/6 + (X^5)/120 - (X^7)/5040 + (X^9)/362880)
    | lgen(R, Y, sin(X)) )).

% accurate to .104 for x = -4.8 .. 0
cnf(sin_lower_bound_2_pos,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, Y, X - (X^3)/6 + (X^5)/120 - (X^7)/5040 + (X^9)/362880 - (X^11)/39916800)
    | lgen(R, Y, sin(X)) )).
