%---- Sqrt Upper Bounds (Based on Newton's Method)
% These bounds are not especially accurate at zero, which causes difficulties with some problems.

% 04-04-2008: checked using Maple
% 09-04-2008: made the bounds more precise, and exact at 1

% This obvious fact benefits some proofs.
cnf(sqrt_squared, axiom, (X < 0 | sqrt(X)*sqrt(X) = X)).

%---- Sqrt Upper Bound (n=0)

cnf(sqrt_upper_bound_0,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, (X + 1)/2, Y)
    | lgen(R,sqrt(X),Y) )).

%---- Sqrt Upper Bound (n=2)

%Argument scaled by a factor of 16: good near zero
cnf(sqrt_upper_bound_2_small,axiom,
    ( ~ 0 <= X
    | ~ X < 1
    | ~ lgen(R, (1/32)*(65536*X^4+114688*X^3+17920*X^2+448*X+1)/((16*X+1)*(256*X^2+96*X+1)), Y)
    | lgen(R,sqrt(X),Y) )).

cnf(sqrt_upper_bound_2,axiom,
    ( ~ 0 <= X
    | ~ lgen(R, (1/8)*(X^4+28*X^3+70*X^2+28*X+1)/((X+1)*(X^2+6*X+1)), Y)
    | lgen(R,sqrt(X),Y) )).

%Argument divided by 16: good for large X
cnf(sqrt_upper_bound_2_large,axiom,
    ( ~ 1 <= X
    | ~ lgen(R, (1/32)*(65536+114688*X+17920*X^2+448*X^3+X^4)/((X+16)*(256+96*X+X^2)), Y)
    | lgen(R,sqrt(X),Y) )).
