%---- Generalised orderings

cnf(lgen_le_pos,axiom,
    ( ~ X <= Y
    | lgen(0,X,Y) )).

cnf(lgen_less_pos,axiom,
    ( ~ X < Y
    | lgen(1,X,Y) )).

cnf(lgen_le_neg,axiom,
    ( X <= Y
    | ~ lgen(0,X,Y) )).

cnf(lgen_less_neg,axiom,
    ( X < Y
    | ~ lgen(1,X,Y) )).

%---- Division axioms

cnf(leq_left_divide_mul_pos,axiom,
    ( ~ X <= Y*Z
    | X/Z <= Y
    | Z <= 0 )).

cnf(leq_left_mul_divide_pos,axiom,
    ( ~ X <= Y/Z
    | X*Z <= Y
    | Z <= 0 )).

cnf(leq_right_divide_mul_pos,axiom,
    ( ~ X*Z <= Y
    | X <= Y/Z
    | Z <= 0 )).

cnf(leq_right_mul_divide_pos,axiom,
    ( ~ X/Z <= Y
    | X <= Y*Z
    | Z <= 0 )).

cnf(leq_left_divide_mul_neg,axiom,
    ( ~ X <= Y*Z
    | Y <= X/Z
    | 0 <= Z )).

cnf(leq_left_mul_divide_neg,axiom,
    ( ~ X <= Y/Z
    | Y <= X*Z
    | 0 <= Z )).

cnf(leq_right_divide_mul_neg,axiom,
    ( ~ X*Z <= Y
    | Y/Z <= X
    | 0 <= Z )).

cnf(leq_right_mul_divide_neg,axiom,
    ( ~ X/Z <= Y
    | Y*Z <= X
    | 0 <= Z )).

%---- Intervals

cnf(interval_intro,axiom,
    ( ~ lgen(R,A,X)
    | ~ lgen(S,X,B)
    | interval(R,A,S,B,X) )).

cnf(interval_elim1,axiom,
    ( ~ interval(R,A,S,B,X)
    | lgen(R,A,X) )).

cnf(interval_elim2,axiom,
    ( ~ interval(R,A,S,B,X)
    | lgen(S,X,B) )).

