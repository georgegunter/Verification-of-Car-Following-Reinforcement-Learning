%---- cosh Lower Bounds

% Based on Taylor series bound for exponentials: exp_lower_taylor_5_cubed, 
%  along with the third continued fraction bound.
% Needed because the continued fraction bounds converge to small values for positive X.

% Consider reducing the range?
% For sinh_taylor_upper_bound_neg1, Avoiding an overlap with other bounds improves efficiency

cnf(cosh_taylor_lower_bound_pos1,axiom,
     ( ~ 0 <= X
     | ~ lgen(R, Y, (1/2)*(1+(1/3)*X+(1/18)*X^2+(1/162)*X^3+(1/1944)*X^4+(1/29160)*X^5)^3 - (1/2)*(X^3-12*X^2+60*X-120)/(X^3+12*X^2+60*X+120))
     | lgen(R,Y,cosh(X)) )).

cnf(cosh_taylor_lower_bound_neg1,axiom,
     ( ~ X < 0
     | ~ lgen(R, Y, (1/2)*(1-(1/3)*X+(1/18)*X^2-(1/162)*X^3+(1/1944)*X^4-(1/29160)*X^5)^3 - (1/2)*(X^3+12*X^2+60*X+120)/(X^3-12*X^2+60*X-120))
     | lgen(R,Y,cosh(X)) )).

%---- These are based on continued fraction approximations to the exponential function.

% the first approximant (valid for all negative X, but very poor beyond -2.5)
cnf(cosh_lower_bound_neg1,axiom,
     ( ~ X < 0
     | ~ -2.5 <= X
     | ~ lgen(R, Y, -(8*(X^2+3)) / ((X-2)*(X^2+6*X+12)))
     | lgen(R,Y,cosh(X)) )).

% the second approximant (valid for all positive X, but very poor beyond 2.5)
cnf(cosh_lower_bound_pos1,axiom,
     ( ~ 0 <= X
     | ~ X <= 2.5
     | ~ lgen(R, Y, (6*(3*X^4+104*X^2+240)) / ((X^2-6*X+12)*(X^3+12*X^2+60*X+120)))
     | lgen(R,Y,cosh(X)) )).

%---- More Accurate Continued Fractions

% the third approximant (valid for all negative X, but very poor beyond -5)
cnf(cosh_lower_bound_neg2,axiom,
     ( ~ X < 0
     | ~ -5 <= X
     | ~ lgen(R, Y, -(32*(X^6+135*X^4+2880*X^2+6300)) / ((X^3-12*X^2+60*X-120)*(X^4+20*X^3+180*X^2+840*X+1680)))
     | lgen(R,Y,cosh(X)) )).

% the fourth approximant (valid for all positive X, but very poor beyond 5)
cnf(cosh_lower_bound_pos2,axiom,
     ( ~ 0 <= X
     | ~ X <= 5
     | ~ lgen(R, Y, (10*(5080320+2378880*X^2+5*X^8+134064*X^4+1800*X^6)) / ((X^4-20*X^3+180*X^2-840*X+1680)*(X^5+30*X^4+420*X^3+3360*X^2+15120*X+30240)))
     | lgen(R,Y,cosh(X)) )).
